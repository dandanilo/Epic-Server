use uo;
use os;

include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":magery:spells";
include "include/client";
include "include/sounds";
include "include/damage";
include "include/epicConstants";
include "include/epicMageryUtils";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
    var spellinfo := params[3];
    var spell_circle := spellinfo.Circle;

	SendSysMessage(who, "Selecione o alvo.");
	if ( !targ.IsA(POLCLASS_MOBILE) )
		SendSysMessage(who, "Alvo invalido.");
		return 0;
	endif

    if (!targ)
		SendSysMessageEx(who, "Cancelado", SSM_FAIL);
		return 0;
	endif
    var mlore := cint(AP_GetSkill(who, MAGICLORE)/25);
    if (TemHabilidade(who, "Evocator"))
        mlore := mlore + 1;
    endif
    var dist := Distance(who, targ);
    var dist_allowed := distanceAllowedForSpells(who, mlore, EVOCATIUM);

    if (dist > dist_allowed)
        SendSysMessage(who, "O alvo esta muito distante");
        return 0;
    endif
	SendSysMessageEx(who, "Misseis magicos saem de sua mao", SSM_INFO);
	var i := 1;
    For (i := 1; i <= mlore; i += 1)
        PlayMovingEffectEx(who, targ, 0x5426, 10, 20, 2290, 2);
        PlaySoundEffect(who, SFX_AGILITY2);
        var dmg := epicSpellDamageRoll(who, spell_circle);
        var total_dmg := bonusSpellDamageForSpecialists(who, dmg, EVOCATIUM);

        DamageFLS(targ, total_dmg, DMG_FORCED, who, "magicmissile");
        SleepMS(500);
    EndFor
	return 1;
endprogram
